plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'com.github.mrbean355'
version '1.7.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.0"
    implementation "io.ktor:ktor-server-netty:1.2.4"
    implementation "io.ktor:ktor-gson:1.2.4"
    implementation 'org.slf4j:slf4j-simple:1.7.28'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.retrofit2:converter-scalars:2.6.1'
    runtimeOnly "org.openjfx:javafx-graphics:${javafx.version}:win"
    runtimeOnly "org.openjfx:javafx-graphics:${javafx.version}:linux"
    runtimeOnly "org.openjfx:javafx-graphics:${javafx.version}:mac"
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}
compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

javafx {
    version = "12.0.2"
    modules = ['javafx.controls', 'javafx.media']
}

mainClassName = 'com.github.mrbean355.admiralbulldog.DotaApplicationKt'

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

task generateBuildConfig() {
    final file = project.file("src/main/kotlin/com/github/mrbean355/admiralbulldog/BuildConfig.kt")
    file.createNewFile()
    file.write("package com.github.mrbean355.admiralbulldog\n" +
            "\n" +
            "@javax.annotation.processing.Generated(\"generateBuildConfig\")\n" +
            "const val APP_VERSION = \"$version\"")
}

compileKotlin.dependsOn(generateBuildConfig)