plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.40'
}

group 'com.github.mrbean355'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "io.ktor:ktor-server-netty:1.1.3"
    compile "io.ktor:ktor-gson:1.1.3"
    compile 'org.slf4j:slf4j-nop:1.7.26'
}

sourceSets.main.kotlin.srcDirs 'build/generated/kotlin'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'com.github.mrbean355.dota2.integration.MainKt'

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

task downloadSoundBytes(type: SoundByteDownloader) {
    soundsDir = "monitored"
}

task generateSoundBytes(type: SoundByteEnumGenerator, dependsOn: downloadSoundBytes) {
    packageName = "com.github.mrbean355.dota2.integration.assets"
    className = "SoundByte"
    monitoredSoundsDir = "monitored"
    unmonitoredSoundsDir = "unmonitored"
}

compileKotlin.dependsOn("generateSoundBytes")
