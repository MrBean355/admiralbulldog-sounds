plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

group 'com.github.mrbean355'
version '1.10.1'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

final fxVersion = '12.0.2'
final platform = findProperty('platform') ?: "win"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.7") {
        // Transitively pulls in Linux dependencies.
        exclude group: 'org.openjfx'
    }
    implementation "io.ktor:ktor-server-netty:1.3.1"
    implementation "io.ktor:ktor-gson:1.3.1"
    implementation 'org.slf4j:slf4j-simple:1.7.30'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'no.tornado:tornadofx:1.7.20'

    implementation "org.openjfx:javafx-base:$fxVersion:$platform"
    implementation "org.openjfx:javafx-controls:$fxVersion:$platform"
    implementation "org.openjfx:javafx-graphics:$fxVersion:$platform"
    implementation "org.openjfx:javafx-media:$fxVersion:$platform"

    testImplementation 'junit:junit:4.13'
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

sourceSets.main.kotlin.srcDirs += 'build/generated'

mainClassName = 'com.github.mrbean355.admiralbulldog.MainKt'

jar {
    baseName "admiralbulldog-sounds-$platform"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
}

task generateBuildConfig() {
    final file = project.file("src/main/kotlin/com/github/mrbean355/admiralbulldog/BuildConfig.kt")
    file.createNewFile()
    file.write("package com.github.mrbean355.admiralbulldog\n" +
            "\n" +
            "import com.vdurmont.semver4j.Semver\n" +
            "\n" +
            "val APP_VERSION: Semver = Semver(\"$version\")\n" +
            "const val DISTRIBUTION: String = \"$platform\"\n")
}

compileKotlin.dependsOn(generateBuildConfig)
